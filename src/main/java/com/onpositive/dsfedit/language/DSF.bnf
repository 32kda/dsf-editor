{
  parserClass="com.onpositive.dsfedit.language.parser.DSFParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="DSF"
  psiImplClassSuffix="Impl"
  psiPackage="com.onpositive.dsfedit.language.parser.psi"
  psiImplPackage="com.onpositive.dsfedit.language.parser.psi.impl"

  elementTypeHolderClass="com.onpositive.dsfedit.language.parser.psi.DSFTypes"
  elementTypeClass="com.onpositive.dsfedit.language.psi.DSFElementType"
  tokenTypeClass="com.onpositive.dsfedit.language.psi.DSFTokenType"

  psiImplUtilClass="com.onpositive.dsfedit.language.parser.psi.impl.DSFPsiImplUtil"
  tokens = [
    OBJECT_DEF_KEYWORD='OBJECT_DEF'
    POLYGON_DEF_KEYWORD='POLYGON_DEF'
    NETWORK_DEF_KEYWORD='NETWORK_DEF'
    PROPERTY_KEYWORD='PROPERTY'
    OBJECT_KEYWORD='OBJECT'
    BEGIN_POLYGON_KEYWORD='BEGIN_POLYGON'
    END_POLYGON_KEYWORD='END_POLYGON'
    BEGIN_WINDING_KEYWORD='BEGIN_WINDING'
    END_WINDING_KEYWORD='END_WINDING'
    POLYGON_POINT_KEYWORD='POLYGON_POINT'
    BEGIN_SEGMENT_KEYWORD='BEGIN_SEGMENT'
    END_SEGMEN_KEYWORD='END_SEGMENT'
    SHAPE_POINT_KEYWORD='SHAPE_POINT'
    SHAPE_POINT_CURVED_KEYWORD='SHAPE_POINT_CURVED'
    I_KEYWORD='I'
    A_KEYWORD='A'

    space='regexp:[ \t]+'
  ]

}

dsfFile ::= dsfHeader declarations entities

ignored ::= comment | eol

dsfHeader ::= ('I'|'A') eol int_num eol id eol

declarations ::= properties object_defs polygon_defs network_defs

properties ::= (property | ignored) *
property ::= 'PROPERTY' space? prop_id value_string

object_defs ::= (('OBJECT_DEF'  value_string) | ignored )*
polygon_defs ::= (('POLYGON_DEF'  value_string) | ignored )*
network_defs ::= (('NETWORK_DEF'  value_string) | ignored )*

private entities ::= entity*
entity ::= object | polygon | segment | eol

object ::= 'OBJECT'  int_num  coords  float_num eol

polygon ::= 'BEGIN_POLYGON'  polygon_header eol (polygon_winding)+ 'END_POLYGON' eol
polygon_header ::= int_num  int_num  int_num
polygon_winding ::= 'BEGIN_WINDING' eol polygon_point+ 'END_WINDING' eol
polygon_point ::= 'POLYGON_POINT'  coords eol
coords ::= float_num  float_num

segment ::= 'BEGIN_SEGMENT'  segment_header  segment_coords eol segment_point* 'END_SEGMENT'  int_num  segment_coords eol
segment_header ::= int_num  int_num  int_num
segment_coords ::= coords  float_num
segment_point ::= simple_point | curved_point
simple_point ::= 'SHAPE_POINT'  coords  float_num eol
curved_point ::= 'SHAPE_POINT_CURVED'  coords  float_num  coords  float_num eol

