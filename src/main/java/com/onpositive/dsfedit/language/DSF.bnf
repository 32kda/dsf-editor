{
  parserClass="com.onpositive.dsfedit.language.parser.DSFParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="DSF"
  psiImplClassSuffix="Impl"
  psiPackage="com.onpositive.dsfedit.language.parser.psi"
  psiImplPackage="com.onpositive.dsfedit.language.parser.psi.impl"

  elementTypeHolderClass="com.onpositive.dsfedit.language.parser.psi.DSFTypes"
  elementTypeClass="com.onpositive.dsfedit.language.psi.DSFElementType"
  tokenTypeClass="com.onpositive.dsfedit.language.psi.DSFTokenType"

  psiImplUtilClass="com.onpositive.dsfedit.language.psi.impl.DSFPsiUtil"
  tokens = [
    OBJECT_DEF_KEYWORD='OBJECT_DEF'
    POLYGON_DEF_KEYWORD='POLYGON_DEF'
    NETWORK_DEF_KEYWORD='NETWORK_DEF'
    PROPERTY_KEYWORD='PROPERTY'
    OBJECT_KEYWORD='OBJECT'
    BEGIN_POLYGON_KEYWORD='BEGIN_POLYGON'
    END_POLYGON_KEYWORD='END_POLYGON'
    BEGIN_WINDING_KEYWORD='BEGIN_WINDING'
    END_WINDING_KEYWORD='END_WINDING'
    POLYGON_POINT_KEYWORD='POLYGON_POINT'
    BEGIN_SEGMENT_KEYWORD='BEGIN_SEGMENT'
    END_SEGMEN_KEYWORD='END_SEGMENT'
    SHAPE_POINT_KEYWORD='SHAPE_POINT'
    SHAPE_POINT_CURVED_KEYWORD='SHAPE_POINT_CURVED'
    I_KEYWORD='I'
    A_KEYWORD='A'

    space='regexp:[ \t]+'
  ]

}

dsfFile ::= dsfHeader declarations entities

private ignored ::= comment | eol

dsfHeader ::= ('I'|'A') eol int_num eol id eol

private declarations ::= properties object_defs polygon_defs network_defs

properties ::= (property | ignored) *
property ::= 'PROPERTY' space? prop_id value_string

object_defs ::= (object_def | ignored )*
object_def ::= 'OBJECT_DEF'  value_string
polygon_defs ::= (polygon_def | ignored )*
polygon_def ::= 'POLYGON_DEF'  value_string
network_defs ::= (network_def | ignored )*
network_def ::= 'NETWORK_DEF'  value_string

private entities ::= entity*
private entity ::= object | polygon | segment | eol

int_ref ::= int_num

object ::= 'OBJECT'  int_ref coords  float_num eol

polygon ::= 'BEGIN_POLYGON' int_ref polygon_header eol (polygon_winding)+ 'END_POLYGON' eol
private polygon_header ::= int_num  int_num
polygon_winding ::= 'BEGIN_WINDING' eol polygon_point+ 'END_WINDING' eol
polygon_point ::= 'POLYGON_POINT'  coords eol
private coords ::= float_num  float_num

segment ::= 'BEGIN_SEGMENT'  segment_header  segment_coords eol segment_point* 'END_SEGMENT'  int_num  segment_coords eol
segment_header ::= int_num  int_num  int_num
private segment_coords ::= coords  float_num
segment_point ::= simple_point | curved_point
simple_point ::= 'SHAPE_POINT'  coords  float_num eol
curved_point ::= 'SHAPE_POINT_CURVED'  coords  float_num  coords  float_num eol

